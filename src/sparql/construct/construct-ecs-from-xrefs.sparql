prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


CONSTRUCT {
  ?c a owl:Class .
  ?c <http://www.w3.org/2002/07/owl#equivalentClass> ?xcURI 
}
WHERE {
 ?c rdfs:subClassOf* <http://purl.obolibrary.org/obo/MONDO_0000001> .
 ?c <http://www.geneontology.org/formats/oboInOwl#hasDbXref> ?x .
 ?a <http://www.w3.org/2002/07/owl#annotatedSource> ?c .
 ?a <http://www.w3.org/2002/07/owl#annotatedProperty> <http://www.geneontology.org/formats/oboInOwl#hasDbXref> .
 ?a <http://www.w3.org/2002/07/owl#annotatedTarget> ?x .
 ?a <http://www.geneontology.org/formats/oboInOwl#source> ?v3 .
 FILTER (strStarts(?v3,"MONDO:equivalent"))
 BIND (
    uri(replace(replace(replace(replace(replace(
        concat("http://purl.obolibrary.org/obo/",
               replace(?x, ":", "_")),
           "http://purl.obolibrary.org/obo/Orphanet_","http://www.orpha.net/ORDO/Orphanet_"
        ), "http://purl.obolibrary.org/obo/OMIM_","https://omim.org/entry/"
        ), "http://purl.obolibrary.org/obo/OMIMPS_","https://omim.org/phenotypicSeries/PS"
    	), "http://purl.obolibrary.org/obo/SCTID_","http://snomed.info/id/"
    	), "http://purl.obolibrary.org/obo/ONCOTREE_","http://oncotree.mskcc.org/api/tumorTypes/search/code/"
      ))
    AS ?xcURI)
}


