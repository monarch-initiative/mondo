PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

# Get classes that have more than 1 subClassOf gene association

SELECT DISTINCT ?entity ?label (GROUP_CONCAT(DISTINCT ?geneIdentifier; separator=", ") AS ?geneIdentifiers)
WHERE {
  ?entity rdfs:subClassOf ?restriction ;
         rdfs:label ?label .

  ?restriction rdf:type owl:Restriction ;
               owl:onProperty obo:RO_0004003 ;
               owl:someValuesFrom ?geneIdentifier .

  # Filter for gene identifiers with HGNC or NCBIGene prefixes or NCBI prefix (for non-human genes)
  FILTER(STRSTARTS(STR(?geneIdentifier), "http://identifiers.org/hgnc/") || STRSTARTS(STR(?geneIdentifier), "http://identifiers.org/ncbigene/"))
}
GROUP BY ?entity ?label
HAVING (COUNT(DISTINCT ?geneIdentifier) > 1)
