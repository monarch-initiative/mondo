PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# Get classes that have more than 1 equivalentTo gene association

SELECT DISTINCT ?entity ?label (GROUP_CONCAT(DISTINCT ?geneIdentifier; separator=", ") AS ?geneIdentifiers)
WHERE {
  ?entity owl:equivalentClass ?equivClass ;
         rdfs:label ?label .

  ?equivClass owl:intersectionOf/rdf:rest*/rdf:first ?component .
  
  ?component rdf:type owl:Restriction ;
             owl:onProperty obo:RO_0004003 ;
             owl:someValuesFrom ?geneIdentifier .

  # Filter for gene identifiers with HGNC prefix or NCBI prefix (for non-human genes)
  FILTER(STRSTARTS(STR(?geneIdentifier), "http://identifiers.org/hgnc/") || STRSTARTS(STR(?geneIdentifier), "http://identifiers.org/ncbigene/"))
}
GROUP BY ?entity ?label
HAVING (COUNT(DISTINCT ?geneIdentifier) > 1)
