PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?entity ?property ?value WHERE {
  VALUES ?property {
    obo:IAO_0000118
    oboInOwl:hasExactSynonym
    rdfs:label
  }
  ?entity ?property ?value.
  ?entity2 ?property ?value .
  
  FILTER NOT EXISTS {
    ?axiom owl:annotatedSource ?entity ;
         owl:annotatedProperty ?property ;
         owl:annotatedTarget ?value ;
         oboInOwl:hasSynonymType <http://purl.obolibrary.org/obo/mondo#ABBREVIATION> .
  }
  
  FILTER NOT EXISTS {
    ?axiom owl:annotatedSource ?entity2 ;
         owl:annotatedProperty ?property ;
         owl:annotatedTarget ?value ;
         oboInOwl:hasSynonymType <http://purl.obolibrary.org/obo/mondo#ABBREVIATION> .
  }
  
  #FILTER NOT EXISTS { ?entity owl:deprecated true }
  #FILTER NOT EXISTS { ?entity2 owl:deprecated true }
  FILTER (?entity != ?entity2)
  FILTER (!isBlank(?entity))
  FILTER (!isBlank(?entity2))
}
ORDER BY DESC(UCASE(str(?value)))