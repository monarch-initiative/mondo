PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix IAO: <http://purl.obolibrary.org/obo/IAO_>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oio: <http://www.geneontology.org/formats/oboInOwl#>
prefix def: <http://purl.obolibrary.org/obo/IAO_0000115>
prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix mondoSparqlQcMondo: <http://purl.obolibrary.org/obo/mondo/sparql/qc/mondo/>
prefix mondo: <http://purl.obolibrary.org/obo/mondo#>


# Tests if an excluded subclass of axiom is asserted between a Mondo class 
# and a non-Mondo class, or between a Mondo class and a class expression

SELECT DISTINCT ?entity ?property ?value WHERE
{
  VALUES ?property { <http://purl.obolibrary.org/obo/mondo#excluded_subClassOf> }
  ?entity ?property ?value .
  FILTER(!isIRI(?value) || !STRSTARTS(STR(?value),"http://purl.obolibrary.org/obo/MONDO_"))
  FILTER( !isBlank(?entity) && STRSTARTS(str(?entity), "http://purl.obolibrary.org/obo/MONDO_"))
  FILTER NOT EXISTS {
    ?entity mondo:excluded_from_qc_check mondoSparqlQcMondo:qc-excluded-subclass.sparql .
  }
}
