prefix owl: <http://www.w3.org/2002/07/owl#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Find subclassOf relationships with source evidence only from Orphanet

SELECT ?term ?term_label ?parent ?parent_label ?source 
WHERE 
{ 
  ?exp owl:annotatedSource ?term ;
       owl:annotatedProperty rdfs:subClassOf ;
       owl:annotatedTarget ?parent;
       oboInOwl:source ?source .

  ?term rdfs:subClassOf ?parent ;
        rdfs:label ?term_label .
  
  ?parent rdfs:label ?parent_label .
  
  FILTER NOT EXISTS { 
    ?exp oboInOwl:source ?source2 .
    FILTER regex(str(?source2), "^(?!Orphanet:)")
  }
  
  FILTER (isIRI(?term) && regex(str(?term), "^http://purl.obolibrary.org/obo/MONDO_"))
  FILTER (isIRI(?parent) && regex(str(?parent), "^http://purl.obolibrary.org/obo/MONDO_"))
  FILTER(regex(str(?source), "^Orphanet:"))
}