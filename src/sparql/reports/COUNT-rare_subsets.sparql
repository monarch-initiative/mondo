PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Get counts for various rare subsets for MONDO:0000001 'disease'

SELECT ?subsetLabel (COUNT(DISTINCT ?class) AS ?count)
WHERE {
  ?class rdfs:subClassOf* obo:MONDO_0000001 ;
         oboInOwl:inSubset ?subset .

  FILTER NOT EXISTS { ?class owl:deprecated true }

  FILTER(STRSTARTS(STR(?class), "http://purl.obolibrary.org/obo/MONDO_"))

  VALUES (?subset ?subsetLabel ?order) {
    (<http://purl.obolibrary.org/obo/mondo#rare>           "rare"            1)
    (<http://purl.obolibrary.org/obo/mondo#nord_rare>      "nord_rare"       2)
    (<http://purl.obolibrary.org/obo/mondo#gard_rare>      "gard_rare"       3)
    (<http://purl.obolibrary.org/obo/mondo#orphanet_rare>  "orphanet_rare"   4)
    (<http://purl.obolibrary.org/obo/mondo#inferred_rare>  "inferred_rare"   5)
    (<http://purl.obolibrary.org/obo/mondo#mondo_rare>     "mondo_rare"      6)
  }
}
GROUP BY ?subsetLabel ?order
ORDER BY ?order
