# Query for NCIT exact matches and subclass evidence
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT ?class ?label ?subClassOf ?subClassOf_source ?exactMatch
WHERE {
  ?class a owl:Class ;
         rdfs:label ?label ;
         rdfs:subClassOf ?subClassOf .

  OPTIONAL {
    ?axiom a owl:Axiom ;
           owl:annotatedSource ?class ;
           owl:annotatedProperty rdfs:subClassOf ;
           owl:annotatedTarget ?subClassOf ;
           oboInOwl:source ?subClassOf_source .
  }

  OPTIONAL {
    ?class skos:exactMatch ?exactMatch .
  }
}
