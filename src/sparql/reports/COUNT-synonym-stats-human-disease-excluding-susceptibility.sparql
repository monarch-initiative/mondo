PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX mondo: <http://purl.obolibrary.org/obo/mondo#>

# Get synonym statistics for MONDO:0000001 'disease' subclasses excluding susceptibility by scope and by xref for exact synonyms

SELECT ?category (COUNT(DISTINCT ?synonym) AS ?count)
WHERE {
  {
    # Synonym counts by scope
    ?class rdfs:subClassOf* obo:MONDO_0000001 ;
           ?synonymProp ?synonym .

    FILTER NOT EXISTS { ?class owl:deprecated true }

    VALUES (?synonymProp ?category) {
      (oboInOwl:hasExactSynonym "exact")
      (oboInOwl:hasNarrowSynonym "narrow")
      (oboInOwl:hasBroadSynonym "broad")
      (oboInOwl:hasRelatedSynonym "related")
    }
  }
  UNION
  {
    # Exact synonyms xref source analysis
    ?class rdfs:subClassOf* obo:MONDO_0000001 ;
           oboInOwl:hasExactSynonym ?synonym .

    FILTER NOT EXISTS { ?class owl:deprecated true }

    ?axiom a owl:Axiom ;
           owl:annotatedSource ?class ;
           owl:annotatedProperty oboInOwl:hasExactSynonym ;
           owl:annotatedTarget ?synonym ;
           oboInOwl:hasDbXref ?xref .

    BIND(
      IF(STRSTARTS(STR(?xref), "OMIM:"), "exact_OMIM",
      IF(STRSTARTS(STR(?xref), "Orphanet:"), "exact_Orphanet",
      IF(STRSTARTS(STR(?xref), "NCIT:"), "exact_NCIT",
      IF(STRSTARTS(STR(?xref), "DOID:"), "exact_DO",
      IF(STRSTARTS(STR(?xref), "ICD10CM:"), "exact_ICD10CM",
      IF(STRSTARTS(STR(?xref), "icd11.foundation:"), "exact_ICD11", "exact_other")))))) AS ?category)

    FILTER(?category != "exact_other")
  }
  UNION
  {
    # Abbreviation-tagged synonyms
    ?class rdfs:subClassOf* obo:MONDO_0000001 ;
           ?synonymProp ?synonym .

    FILTER NOT EXISTS { ?class owl:deprecated true }

    ?axiom a owl:Axiom ;
           owl:annotatedSource ?class ;
           owl:annotatedProperty ?synonymProp ;
           owl:annotatedTarget ?synonym ;
           oboInOwl:hasSynonymType mondo:ABBREVIATION .

    VALUES (?synonymProp ?category) {
      (oboInOwl:hasExactSynonym "exact_abbreviation")
      (oboInOwl:hasNarrowSynonym "narrow_abbreviation")
      (oboInOwl:hasBroadSynonym "broad_abbreviation")
      (oboInOwl:hasRelatedSynonym "related_abbreviation")
    }
  }

  # Exclude disease susceptibility subclasses
  MINUS {
    SELECT DISTINCT ?class WHERE {
      # MONDO:0042489 'disease susceptibility'
      ?class rdfs:subClassOf* obo:MONDO_0042489 .
    }
  }
}
GROUP BY ?category
ORDER BY ?category
