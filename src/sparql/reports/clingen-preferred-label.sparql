PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX OMO: <http://purl.obolibrary.org/obo/OMO_>

# description: Creates a list of all CLINGEN labels. Given OMO annotation, the query only works on OWL files

SELECT ?entity ?label (GROUP_CONCAT(?value;SEPARATOR="|") AS ?values) ?synonym_property
WHERE {
  ?entity rdfs:label ?label .
  [] owl:annotatedSource ?entity ;
         owl:annotatedProperty ?synonym_property ;
         owl:annotatedTarget ?value ;
         OMO:0002001 "https://w3id.org/information-resource-registry/clingen" .

  FILTER (isIRI(?entity) && STRSTARTS(str(?entity), "http://purl.obolibrary.org/obo/MONDO_"))	
} 
GROUP BY ?entity ?label ?synonym_property
