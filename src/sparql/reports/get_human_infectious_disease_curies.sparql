PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Get all non-obsolete Mondo subclasses of MONDO:0005550 'infectious disease'

SELECT DISTINCT ?curie ?label
WHERE {
  #  'infectious disease' MONDO_0005550
  ?class rdfs:subClassOf* obo:MONDO_0005550 .
  
  FILTER NOT EXISTS { ?class owl:deprecated true }
  FILTER (isIRI(?class) && STRSTARTS(str(?class), "http://purl.obolibrary.org/obo/MONDO_"))

  OPTIONAL { ?class rdfs:label ?label }

  BIND(REPLACE(STR(?class), "http://purl.obolibrary.org/obo/MONDO_", "MONDO:") AS ?curie)
}
ORDER BY ?curie
