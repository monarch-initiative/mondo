PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?sub ?sub_label ?super ?super_label WHERE {
    ?sub rdfs:subClassOf ?super .
  	?sub rdfs:label ?sub_label .
  	?super rdfs:label ?super_label .
  
  FILTER NOT EXISTS {
    ?xref_anno a owl:Axiom ;
           owl:annotatedSource ?sub ;
           owl:annotatedProperty rdfs:subClassOf ;
           owl:annotatedTarget ?super ;
           oboInOwl:source ?source .
  }
  
  	FILTER(?sub!=?super)
    FILTER(CONTAINS(?sub_label, ?super_label))
    FILTER (isIRI(?super) && STRSTARTS(str(?super), "http://purl.obolibrary.org/obo/MONDO_"))
}
