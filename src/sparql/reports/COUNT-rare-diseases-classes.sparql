PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Count of unique non-obsolete Mondo classes that are in the rare disease subset

SELECT (COUNT(DISTINCT ?class) AS ?countRareSubset)
WHERE {
  ?class rdfs:subClassOf* obo:MONDO_0000001 ;
         oboInOwl:inSubset <http://purl.obolibrary.org/obo/mondo#rare> .
  
  FILTER NOT EXISTS {
    ?class owl:deprecated "true"^^xsd:boolean
  }

  FILTER (isIRI(?class) && STRSTARTS(str(?class), "http://purl.obolibrary.org/obo/MONDO_"))
}
