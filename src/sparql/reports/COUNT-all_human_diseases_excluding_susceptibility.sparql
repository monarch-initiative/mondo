PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>


# Count of all non-obsolete 'human disease' classes excluding 'disease susceptibility' subclasses

SELECT (COUNT(DISTINCT ?class) AS ?countHumanDiseaseExcludingSusceptibility)
WHERE {
  {
    SELECT DISTINCT ?class WHERE {
      ?class rdfs:subClassOf* obo:MONDO_0700096 .
      FILTER(STRSTARTS(STR(?class), "http://purl.obolibrary.org/obo/MONDO_"))
      FILTER NOT EXISTS { ?class owl:deprecated true }
    }
  }
  MINUS {
    SELECT DISTINCT ?class WHERE {
      # MONDO:0042489 'disease susceptibility'
      ?class rdfs:subClassOf* obo:MONDO_0042489 .
    }
  }
}